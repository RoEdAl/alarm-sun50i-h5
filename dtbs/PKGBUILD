# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
pkgver=5.6.p1
pkgrel=1
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs')
_srcname=linux-5.6
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>5.6')
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=de0a95bc6f320b4067d9700b79dd9874c4ce9232

_patches=(
	'0001-arm-dts-sunxi-OrangePi-Zero-Plus-improve-WiFi-bindin.patch'
	'0002-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config"
	${_patches[@]})
md5sums=('7b9199ec5fa563ece9ed585ffb17798f'
         'be2547f1a2229707e8ab4bd0f02cb5bb'
         'f08cb405d7202174b2d5e8267f51ceca'
         '4c17f111a9807883e473cfb02af9e2fd')

prepare() {
  cd "${srcdir}/${_srcname}"

  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun50i-h5-nanopi-neo2'
    'sun50i-h5-orangepi-zero-plus'
    'sun50i-h5-orangepi-pc2'
    'sun50i-h6-orangepi-one-plus'
  )
  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm64/boot/dts/allwinner/${i}.dtb "${pkgdir}/${_dstdir}"
  done
}
