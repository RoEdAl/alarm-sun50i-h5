#
# Additional DTBs for Allwinner H5 SoCs
#

pkgbase='sun50i-h5-dtbs'
pkgname=('orangepi-dtbs' 'nanopi-dtbs')
pkgver=4.15
pkgrel=2
_srcname=linux-4.15
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=81478b9c124ef13c3749b40fcf9ef544d24b6631

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	'0001-arm64-dts-sunxi-Orange-Pi-Zero-Plus.patch'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config")
md5sums=('0d701ac1e2a67d47ce7127432df2c32b'
         'e43f9443866d793a6c9fc03400b196d1'
         'f5183ec7a29254844369b08271f360d7')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  git apply ../0001-arm64-dts-sunxi-Orange-Pi-Zero-Plus.patch

  cat "${srcdir}/config" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"
  
  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs/allwinner"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-nanopi-neo2.dtb "${pkgdir}/boot/dtbs/allwinner/sun50i-h5-nanopi-neo2_.dtb"
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs/allwinner"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-zero-plus.dtb "${pkgdir}/boot/dtbs/allwinner"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-pc2.dtb "${pkgdir}/boot/dtbs/allwinner/sun50i-h5-orangepi-pc2_.dtb"

}
