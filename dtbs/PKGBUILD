#
# Additional DTBs for Allwinner H5 SoCs
#

pkgbase='sun50i-h5-dtbs'
pkgname=('orangepi-dtbs' 'nanopi-dtbs')
pkgver=4.16
pkgrel=2
_srcname=linux-4.16
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=88ed719293e141e6b5bb0b3d1f9f68789be5351f

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	'0001-arm64-dts-sunxi-Orange-Pi-Zero-Plus.patch'
	'0002-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config")
md5sums=('1357fb4ee7c288fdeac5d4e0048f5c18'
         '1ea305d1520d2b924ec889a34a81b393'
         '95ba21ba5368e6c75da148fec1c563f5'
         'f0e6403851b62184ade37074e06ab622')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  git apply ../0001-arm64-dts-sunxi-Orange-Pi-Zero-Plus.patch
  git apply ../0002-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch

  cat "${srcdir}/config" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"
  
  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra/allwinner"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-nanopi-neo2.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra/allwinner"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-zero-plus.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-pc2.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
}
