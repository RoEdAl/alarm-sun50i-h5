# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
pkgver=5.1.p1
pkgrel=1
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs')
_srcname=linux-5.1
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>5.1')
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=b54f29537ea435daf6efe0ead99e37cf7238cbd3

_patches=(
	'0001-arm-dts-sunxi-OrangePi-Zero-Plus-improve-WiFi-bindin.patch'
	'0002-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config"
	${_patches[@]})
md5sums=('15fbdff95ff98483069ac6e215b9f4f9'
         'bc8804313dfd92134ad586f5797c918d'
         'd97a45949d4b487768ce319479bbd222'
         '9f43c19314bff49bd601ec4cd31f78c2')

prepare() {
  cd "${srcdir}/${_srcname}"

  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun50i-h5-nanopi-neo2.dtb'
    'sun50i-h5-orangepi-zero-plus.dtb'
    'sun50i-h5-orangepi-pc2.dtb'
  )
  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm64/boot/dts/allwinner/${i} "${pkgdir}/${_dstdir}"
  done
}
