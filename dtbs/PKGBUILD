#
# Additional DTBs for Allwinner H5 SoCs
#

pkgbase='sun50i-h5-dtbs'
pkgname=('orangepi-dtbs' 'nanopi-dtbs')
pkgver=4.17
pkgrel=1
_srcname=linux-4.17
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=ce807c2612aae93f97ecda19ab7af6ad17152834

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	'0001-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config")
md5sums=('5bb13a03274b66b56c85b26682e407d7'
         '4646b7bb9670945d0289f07f75ee6c62'
         '02251a347bc3f8daa61cd8bbb8883584')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  git apply ../0001-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch

  cat "${srcdir}/config" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"
  
  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra/allwinner"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-nanopi-neo2.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra/allwinner"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-zero-plus.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-pc2.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
}
