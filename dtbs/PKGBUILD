# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
pkgver=4.19.0
pkgrel=1
pkgdesc='Additional DTBs for Allwinder-based boards'
_srcname=linux-4.19
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>=4.19.0')
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=5dbd7f23036728f1e7fab56451f7416ccceccbf7

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config")
md5sums=('740a90cf810c2105df8ee12e5d0bb900'
         '1fadeca6502c809ada798982be336e37')

prepare() {
  cd "${srcdir}/${_srcname}"

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"
  
  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local _dstdir=usr/share/${pkgname}
  mkdir -p "${pkgdir}/${_dstdir}"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-nanopi-neo2.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-zero-plus.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-pc2.dtb "${pkgdir}/${_dstdir}"
}
