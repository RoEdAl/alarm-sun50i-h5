#
# Additional DTBs for Allwinner H5 SoCs
#

pkgbase='sun50i-h5-dtbs'
pkgname=('orangepi-dtbs' 'nanopi-dtbs')
pkgver=4.18
pkgrel=1
_srcname=linux-4.18
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=bab1a57cfb48c5b63eba2343ad9489df709d4fff

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	'0001-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config")
md5sums=('bee5fe53ee1c3142b8f0c12c0d3348f9'
         '39640ed0637aed9544f5844ee9c2981a'
         '96969a438a2a4e39df5ed44e8ba68c03')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  git apply ../0001-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch

  cat "${srcdir}/config" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"
  
  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra/allwinner"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-nanopi-neo2.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra/allwinner"

  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-zero-plus.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
  cp arch/arm64/boot/dts/allwinner/sun50i-h5-orangepi-pc2.dtb "${pkgdir}/boot/dtbs-extra/allwinner"
}
