# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgbase='uboot-sun50i-h5'
pkgname=('uboot-nanopi-neo2' 'uboot-orangepi-zero-plus' 'uboot-orangepi-pc2')
pkgver=2018.05
pkgrel=5
arch=('aarch64')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
options=('!strip')
makedepends=('bc' 'python2' 'swig' 'dtc')
backup=(
	'boot/boot.txt'
	'boot/boot.scr')
source=('atf::git+https://github.com/apritzel/arm-trusted-firmware.git#branch=allwinner'
	"ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
	'nanopi_neo2_defconfig'
	'orangepi_zero_plus_defconfig'
	'orangepi_pc2_defconfig'
	'config-fragment'
	'sun50i-h5-orangepi-zero-plus.dts'
	'boot.txt'
	'mkscr.sh'
	'flash-uboot.sh')
md5sums=('SKIP'
         'ad5c59bc19724c1679feb2a0c9ff4a25'
         '8113a11591e0c5033478b9293f4b4f25'
         '0e0f7c7347022007af43c5225a12f8f2'
         'aaa246fbca5cbd34d18a674521dd289f'
         '9b41dd5fd3c329dc59d3c74bff45d409'
         'fae0f6c05aebd63a2c68b793f0c90841'
         '741e729f28b8cfffb1b85a2588e4eb1f'
         'd46c501c5e750bcdf53263733ab94f9f'
         '2881da461476253d93a74a7c4a4c7ad3')

_boards=('nanopi_neo2' 'orangepi_zero_plus' 'orangepi_pc2')

prepare() {
  cd u-boot-${pkgver}

  cp ${srcdir}/nanopi_neo2_defconfig configs

  cp ${srcdir}/orangepi_zero_plus_defconfig configs
  cp ${srcdir}/sun50i-h5-orangepi-zero-plus.dts arch/arm/dts

  cp ${srcdir}/orangepi_pc2_defconfig configs
}

build() {
  # http://github.com/u-boot/u-boot/blob/master/board/sunxi/README.sunxi64

  local MAKEOPTS='V=0'

  # ATF - modified environments
  cd atf
  env -u CXXFLAGS -u LDFLAGS CFLAGS=-fno-stack-protector make PLAT=sun50iw1p1 DEBUG=0 bl31 ${MAKEOPTS}

  # U-Boot - unmodified environments
  cd ../u-boot-${pkgver}

  for i in ${_boards[@]}; do
    mkdir ../bin_${i}
    make distclean
    make ${i}_config
    scripts/kconfig/merge_config.sh .config ${srcdir}/config-fragment
    make EXTRAVERSION=-${pkgrel} PYTHON=python2 BL31=${srcdir}/atf/build/sun50iw1p1/release/bl31.bin ${MAKEOPTS}
    cat spl/sunxi-spl.bin u-boot.itb > ../bin_${i}/u-boot-sunxi-with-spl.bin
  done

  # boot script - arm64 architecture
  tools/mkimage -A arm64 -O linux -T script -C none -n 'U-Boot boot script' -d ../boot.txt ../boot.scr
}

package_uboot-nanopi-neo2() {
  pkgdesc='U-Boot for NanoPi Neo2'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi')
  optdepends=('nanopi-dtbs' 'uboot-tools' 'sun50i-h5-dt-overlays')

  install -d "${pkgdir}/boot"
  install -Dm644 bin_nanopi_neo2/u-boot-sunxi-with-spl.bin "${pkgdir}/boot"

  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr.sh "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
}

package_uboot-orangepi-zero-plus() {
  pkgdesc='U-Boot for Orange Pi Zero Plus'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi')
  optdepends=('orangepi-dtbs' 'uboot-tools' 'sun50i-h5-dt-overlays')

  install -d "${pkgdir}/boot"
  install -Dm644 bin_orangepi_zero_plus/u-boot-sunxi-with-spl.bin "${pkgdir}/boot"

  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr.sh "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
}

package_uboot-orangepi-pc2() {
  pkgdesc='U-Boot for Orange Pi PC2'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi')
  optdepends=('orangepi-dtbs' 'uboot-tools' 'sun50i-h5-dt-overlays')

  install -d "${pkgdir}/boot"
  install -Dm644 bin_orangepi_pc2/u-boot-sunxi-with-spl.bin "${pkgdir}/boot"

  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr.sh "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
}

